rem Ritimba
rem Copyright (C) 2011 Marcos Cruz (programandala.ne)

defproc credits
  cls_ black,white,blue
  at #ow,1,0:print #ow,"RITIMBA"\\"Por:"\"Marcos Cruz (programandala.net),"\"2011"
  print #ow,\\"Una version en SuperBASIC de..."
  print #ow,"DICTADOR, de Don priestley, 1983"
enddef

defproc fx_war
enddef

defproc fx_2
enddef

rem _________________________________________________
rem Main

defproc ritimba

  loc a

  init_once

  rep again

    credits
    let score=0
    let decisions=49
    let groups=8
    let local_groups=6

    rem Group ids
    let army=1
    let peasants=2
    let landowners=3
    let guerrillas=4
    let leftoto=5
    let police=6
    let russia=7
    let usa=8

    rem Group fields in group$()
    let popularity=1
    let power=2

    rem Decision fields in decision$()
    let cost=2
    let monthly_cost=3

    let decision_max_len=49:rem todo!!! check on reading
    dim decision$(decisions,decision_max_len)
    let group_max_len=23:rem todo!!! check on reading
    dim group$(groups,group_max_len)
    dim group_short_name$(groups,10)

    restore
    for a=1 to decisions: read decision$(a)
    wait_key_press
    title

    rem restore 6150:rem needed?!!!
    for a=1 to groups: read group$(a)
    for a=1 to groups: read group_short_name$(a)
    for a=1 to decisions: let decision$(a,1)="N"
    let money=1000
    let escape=0
    let month_payment=60
    let strength=4
    let money_in_switzerland=0
    let alive=10
    let months=0
    let pc=0
    let revolution_strength=10
    cls_ white,black,blue
    paper #ow,cyan:center #ow,1,"Bienvenido al cargo"
    paper #ow,white
    print #ow,"El anterior dictador de nuestra"
    print #ow,"amada patria Ritimba tuvo una"
    print #ow,"puntuacion final de ";score;"."
    if score<=0
      print #ow,\\"Como este es tu primer intento"\"no dudes en hacerlo mejor!"
    else
      print #ow,\"Intenta al menos acabar con ";score+1;"!"
    endif
    print #ow,\\"Comienza con un informe del"
    print #ow,"tesoro y con otro de la policia."
    wait_key_press
    treasure_report
    wait_key_press
    police_report_step_2

    rep game

      new_month
      audience
      plot
      murder: if not alive:exit game
      war: if not alive or escape:exit game
      plot
      police_report
      decision
      plot
      police_report
      news
      revolution:if not alive or escape:exit game

    endrep game

    the_end

  endrep again

enddef

defproc audience

  loc a,c

  cls_ yellow,black,yellow
  paper #ow,green
  print #ow,blank_line$;blank_line$;blank_line$;blank_line$;blank_line$
  paper #ow,white:ink #ow,black
  center #ow,3,"UNA AUDIENCIA"

  rep choose_decision
    let this_decision=rnd(1 to 24)
    for a=1 to 22
      if decision$(this_decision,1)="N"
        exit choose_decision
      endif
      let this_decision=(this_decision-int(this_decision/24)*24)+1
    end for a
    for a=1 to 24:let decision$(a,1)="N"
  endrep choose_decision

  let decision$(this_decision,1)="*"
  let gs=int((this_decision-1)/8)+1
  zx_border gs
  let this_group$=group$(gs,6 to code(group$(gs,5))-20)
  let this_decision$=decision$(this_decision)
  paper #ow,yellow:ink #ow,black
  center #ow,10,"Peticion de "&this_group$&":"
  center #ow,14,"Esta su excelencia conforme en"
  paper #ow,white
  center #ow,16,this_decision$(18 to)
  wait_key_press: advert this_decision

  cls #ow
  paper #ow,white
  center #ow,1,"  DECISION  "
  paper #ow,gs:ink #ow,contrast_colour(gs)
  center #ow,3,"Peticion de "&this_group$
  paper #ow,yellow:ink #ow,black
  center #ow,5,this_decision$(18 to )
  paper #ow,blue
  print #ow,\blank_line$

  let c=cash_advice(this_decision)
  print #ow,: ink #ow,green
  print #ow,blank_line$
  if c=0
    cls #ow
    at #ow,10,1
    print #ow,"No tiene suficientes fondos"
    print #ow,\\" para";
    print #ow," pagar esta decision."
    at #ow,15,4:print #ow,"Su respuesta debe ser no"
    pause 250
  else
    if yes_key:ret
  endif

  let x=group$(gs,popularity)
  let y=code(this_decision$(gs+3))-77
  let x=x-y
  if x<0:let x=0
  let group$(gs,popularity)=x
  cls #ow
  treasure_report

enddef

defproc treasure_report
  cls_ white,green,green
  print #ow,fill$("$",columns*ow_lines)
  ink #ow,black
  treasure_report_details
enddef

defproc treasure_report_details
  at #ow,8,7
  print #ow,"INFORME DEL TESORO"
  treasure_report_details_2
enddef

defproc print_money(money)
  print_using #ow,"###.###.###,",money*1000
  print #ow," RTD";:rem temp currency symbol!!!
enddef

defproc treasure_report_details_2
  paper #ow,blue: ink #ow,white
  at #ow,12,1:print #ow," El tesoro";
  if int(money)>=0
    print #ow," tiene";
  else
    print #ow," ";:print #ow,"debe";
  endif
  print_money abs(int(money))
  at #ow,14,0:print #ow,"Los gastos mensuales son ";
  print_money month_payment
  if money_in_switzerland>0
    at #ow,17,2
    print #ow,"[En suiza tiene $";:print_money money_in_switzerland:print #ow,"]"
  endif
enddef

defproc new_month

  let low=rnd(2 to 4)
  let revolution_strength=rnd(10 to 12)
  let months=months+1

  cls_ yellow,black,yellow
  paper #ow,cyan:ink #ow,white
  at #ow,10,12:print #ow,"MES  ";
  paper #ow,white: ink #ow,black
  print #ow,months
  pause 50
  plot
  if money<=0
    bankruptcy
  else
    let money=money-month_payment
  endif
enddef

defproc bankruptcy

  cls #ow
  center #ow,5,"El tesoro esta en bancarrota"
  at #ow,9,0:print #ow,"Su popularidad con el ejercito y"\\
  print #ow,"con la policia secreta caera!"\\\
  print #ow,"El poder de la policia bajara"\\
  print #ow,"y tu propio poder tambien"
  if group$(army,popularity)>"0"
    let group$(army,popularity)=group$(army,popularity)-1
  endif
  if group$(police,popularity)>"0"
    let group$(police,popularity)=group$(police,popularity)-1
  endif
  if group$(police,power)>"0"
    let group$(police,power)=group$(police,power)-1
  endif
  if strength>0:let strength=strength-0
  pause 250
  plot
  police_report

enddef

defproc advert(decision)

  loc a

  cls_ green,black,blue
  paper #ow,cyan
  for a=0 to ow_lines-1
    at #ow,a,11:print #ow,"aviso?"
  endfor a
  if yes_key
    cls_ yellow
    paper #ow,black:ink #ow,yellow
    at #ow,1,0
    print #ow,decision$(decision,18 to )
    paper #ow,white:ink #ow,black
    at #ow,3,0
    print #ow,"Su popularidad entre..."
    paper #ow,yellow
    for a=1 to groups
      let x=code(decision$(decision,a+3))-77
      if x=0:next a:exit a
      print #ow,\to 2;group$(a,6 to );to 21;
      if x>0:print #ow,"+";
      print #ow,x;
      if gs=a and decision<25
        paper #ow,gs:ink #w,yellow
        print #ow,"< "
        paper #ow,yellow:ink #ow,black
      endif
    endfor a
    paper #ow,white
    print #ow,\\\"El poder de..."
    paper #ow,yellow
    for a=1 to local_groups
      let x=code(decision$(decision,a+11))-77
      if x=0:next a:exit a
      print #ow,to 2;group$(a,6 to );to 21;
      if x>0:print #ow,"+";
      print #ow,x
    endfor a
    wait_key_press
    cls #ow
  endif
enddef

defproc cls_(paper_colour,ink_colour,border_colour)
  paper #ow,paper_colour
  ink #ow,ink_colour
  zx_border border_colour: cls #ow
  paper #iw,border_colour: cls #iw
  let prompt_colour_1=border_colour
  let prompt_colour_2=paper_colour
  if prompt_colour_1=prompt_colour_2
    let prompt_colour_2=contrast_colour(prompt_colour_1)
  endif
  zx_beep .1,40
enddef

defproc plot

  loc a,p

  if months<=2 or months<pc:ret

  for a=1 to 3: let group$(a,3 to 4)="::"

  for a=1 to 3
    if group$(a,popularity)<=low
      for p=1 to local_groups
        if not(a=p or group$(p,popularity)>low)
          if group$(p,power)+group$(a,power)>=revolution_strength
            let group$(a,3)="R"
            let group$(a,4)=p
            exit p
          endif
        endif
      next p
        let group$(a,3)="A"
      endfor p
    endif
  endfor a

enddef

defproc murder

  loc group

  let group=rnd(1 to 3)
  if group$(group,3)="A"

    cls_ black,white,black
    at #ow,10,7:print #ow,"intento de asesinato"
    at #ow,20,0:print #ow,"por uno de ";group$(group,6 to )
    cls #ow
    pause 30: fx_2: pause 50

    if ((group$(army,3)="A" and group$(peasants,3)="A" and group$(landowners,3)="A") or not (group$(police,popularity)>low or group$(police,power)>low or rnd(0 to 1))):rem check the compacted logic!!!
      cls_ black,white,black
      center #ow,12,"Esta usted muerto!"
      zx_beep 3,-40
      let alive=0
    else
      cls_ white,black,black
      paper #ow,white:ink #ow,black
      center #ow,12," Intento fallido "
    endif

  endif

enddef

defproc tune(m$)

  loc note

  for note=1 to len(m$) step 2
    if m$(note+1)=" "
      pause code(m$(note))/4
      next note:exit note
    endif
    zx_beep (code(m$(note))-48)/6,code(m$(note+1))-105
  endfor note

enddef

defproc take_only_once_decision(decision):rem rename!!!
  let decision$(decision,1)="*"
  take_decision
enddef

defproc take_decision(decision):rem rename!!!

  loc group,t$,x

  let t$=decision$(decision,4 to 11)
  for group=1 to groups
    if t$(group)<>"M":rem M means 0
      let x=group$(group,popularity)+(code(t$(group))-77)
      if x>9:let x=9
      if x<0:let x=0
      let group$(group,popularity)=x
    endif
  endfor group

  let t$=decision$(decision,12 to 17)
  for group=1 to local_groups
    if t$(group)<>"M":rem M means 0
      let x=group$(group,power)+(code(t$(group))-77)
      if x>9:let x=9
      if x<0:let x=0
      let group$(group,power)=x
    endif
  endfor group

  rem move:!!! it seems this is month stuff:
  let money=money+decision_cost
  let month_payment=month_payment-decision_monthly_cost
  if month_payment<0:let month_payment=0

enddef

defproc police_report

  cls_ black,white,black
  if money<=0 or group$(police,popularity)<=low or group$(police,power)<=low
    police_report_not_avalaible
  else
    center #ow,6,"INFORME de la POLICIA SECRETA?"
    center #ow,12,"(cuesta 1.000 $)"
    if yes_key
      let money=money-1
      police_report_step_2
    endif
  endif
enddef

defproc police_report_step_2
  cls_ black,white,black
  print #ow,"MES ";months
  ink #ow,blue
  at #ow,3,0:print #ow,blank_line$:rem !!!
  police_report_step_3
enddef

defproc police_report_step_3

  loc group

  paper #ow,white:ink #ow,black
  center #ow,3,"INFORME de la POLICIA SECRETA"
  paper #ow,black:ink #ow,white
  at #ow,6,1:print #ow,"POPULARIDAD"
  at #ow,6,22:print #ow,"FUERZA"
  for group=1 to groups
    at #ow,7+group,11
    paper #ow,yellow: ink #ow,black
    flash #ow,(group$(group,3)="R"):rem change into strip!!!
    print #ow,group_short_name$(group)
    paper #ow,white
    at #ow,7+group,10:print #ow,group
    if group<=3 and group$(group,3)="R"
      paper #ow,white
      at #ow,7+group,21
      print #ow,group$(group,4)
    endif
    let x=group$(group,popularity)
    paper #ow,green:ink #ow,white
    at #ow,7+group,10-x
    print #ow,"987654321"(10-x to )
    if group<=3 and group$(group,3)="A"
      paper #ow,white
      at #ow,group+7,21
      print #ow,"A"
    endif
  endfor group
  for group=1 to local_groups
    at #ow,7+group,22
    paper #ow,red: ink #ow,white
    print #ow,"123456789"(to group$(group,power))
  endfor group
  paper #ow,black:ink #ow,white
  at #ow,17,0
  print #ow,"Tu FUERZA es ";strength
  at #ow,19,0
  print #ow,"La FUERZA de la REVOLUCION es ";revolution_strength
  wait_key_press
  cls_ green,black,green
enddef

defproc police_report_not_avalaible

  center #ow,6,"INFORME de la POLICIA SECRETA"
  center #ow,10,"NO DISPONIBLE"

  if group$(police,popularity)<=low
    print #ow,\\"Tu popularidad entre nosotros es ";group$(police,popularity);"."
  endif
  if group$(police,power)<=low
    print #ow,to 3\\"El poder de la policia es ";group$(police,power);"."
  endif
  if money<=0
    print #ow,to 3\\"No tienes dinero para pagar un informe."
  endif

enddef

defproc revolution

  loc a,helping_group,helping_groups,rebel_group,try_escaping

  for a=1 to 3
    let rebel_group=rnd(1 to 3)
    if group$(rebel_group,3)="R":exit a
  next a
    ret
  endfor a

  cls_ red,black,red
  ink #ow,white
  center #ow,10,"REVOLUCION"
  for a=1 to 5: zx_beep .5,20: zx_beep .5,10
  cls_ yellow,black,yellow

  center #ow,12,"Intento de escape?"
  let try_escaping=yes_key
  cls #ow
  if try_escaping

    if decision$(36,1)="*":rem helicopter
      if rnd(0 to 2)
        center #ow,12,"Escapas en helicoptero!"
        let escape=1
        ret
      else
        center #ow,10,"El helicoptero no funciona!"
        pause 150
      endif
    endif

    at #ow,10,2:print #ow,"Tienes que atravesar el"
    at #ow,12,6:print #ow,"monte hacia leftoto"
    pause 200
    cls #ow

    if not int((rnd*((group$(guerrillas,power)/3)+.4)))
      at #ow,12,0: print #ow,"  Las guerillas no te cojieron  "
      let escape=1
    else
      cls_ black,white,black
      pause 50: fx_2
      at #ow,12,0:print #ow,"Las guerillas estan celebrandolo"
      fx_2
      let alive=0
    endif
    ret

  endif:rem escape

  rep ask_for_help

    pause 150
    cls #ow 

    let x=group$(rebel_group,power)+group$(group$(rebel_group,4),power)
    at #ow,5,0:print #ow,group$(rebel_group,6 to code(group$(rebel_group,5))-20);" se ha unido a "
    print #ow,group$(group$(rebel_group,4),6 to )\\
    print #ow,"Su fuerza unidos es ";x
    print #ow,\\"a quien vas a pedir ayuda?"
    let helping_groups=0
    for a=1 to local_groups
      if group$(a,popularity)>low
      print #ow,to 6;a;" .";group$(a,6 to code(group$(a,5))-20);" ?"
      let helping_groups=helping_groups+1
      endif
    endfor a

    if helping_groups
      rep choose_group
        let k$=get_key$
        if k$ instr "123456":exit choose_group
      endrep choose_group
      if group$(k$,popularity)>low
        let helping_group=k$: exit ask_for_help
      else
        cls #ow
        center #ow,12,"Debes estar bromeando!"
        next ask_for_help
      endif
    else
      cls #ow
      center #ow,8,"estas solo!"
      wait_key_press
    endif
    exit ask_for_help

  endrep ask_for_help

  cls #ow

  at #ow,8,0:print #ow,"Tu fuerza es ";strength
  print #ow,\\"La fuerza de ";group$(helping_group,6 to code(group$(helping_group,5))-20);" es ";group$(helping_group,power)
  print #ow,\\"La de los revolucionarios es ";x
  pause 250
  cls_ white,black,white
  at #ow,12,3:print #ow,"La revolucion ha comenzado"
  fx_war
  if not(x<=strength+group$(helping_group,power)+rnd(-1 to 1))
    cls_ black,white,black: at #ow,10,7:print #ow,"Has sido derrocado"
    at #ow,12,10:print #ow,"y ";: print #ow,"liquidado"
    fx_2
    let alive=0:ret
  endif

  cls_ black,white,black
  at #ow,10,2:print #ow,"La revuelta ha sido sofocada"
  at #ow,0,0
  for a=1 to 22
    paper #ow,rnd(1 to 5)
    print #ow,blank_line$
  endfor a
  at #ow,10,0: print #ow,"castigas a los revolucionarios?"
  if yes_key
    for n=1 to 3: fx_2: pause .1
    let group$(rebel_group,1 to 2)="00"
    let group$(group$(rebel_group,4),1 to 2)="00"
  endif
  let group$(helping_group,power)="9"
  let pc=months+2
  plot
  police_report

enddef

deffn cash_advice(decision)

  rem loc decision_cost

  paper #ow,yellow:ink #ow,black
  let decision_cost=10*(code(decision$(decision,cost))-77)
  let decision_monthly_cost=code(decision$(decision,monthly_cost))-77
  if not decision_cost and not decision_monthly_cost:at #ow,10,7:print #ow,"no cuesta dinero": ret 1

  at #ow,9,1:print #ow,"Esta decision ": print

  if decision_cost
    if decision_cost>0:print #ow," daria al";
    if decision_cost<0:print #ow," costaria al";
    print #ow,"  tesoro ";abs(decision_cost);" 000 $": print
    if monthy_cost:print #ow," y": print
  endif

  if decision_monthly_cost
    if decision_monthly_cost<0:print #ow," subiria";
    if decision_monthly_cost>0:print #ow," bajaria";
    print #ow," los costos mensuales en ";abs(decision_monthly_cost);" 000 $"
  endif

  if money+decision_cost>0: ret 1
  if not((decision_cost<0 or decision_monthly_cost<0) and (money+decision_cost<0 or money+decision_monthly_cost<0)): ret 1

  pause 250: cls #ow
  center #ow,5,decision$(decision,18 to )
  at #ow,8,2:print #ow,"El dinero necesario"
  print #ow,\to 4;"no esta en el tesoro"\\\

  if decision$(38,1)="N":print #ow,"Quizas los rusos pueden ayudar?": print
  if decision$(39,1)="N":print #ow,"Los americanos son un pueblo"\"generoso": print

  pause 350
  ret 0

enddef

defproc ask_for_loan(decision)

  loc country,loan

  sel on decision:rem tmp!!! make better
    =38:let country=russian
    =39:let country=usa
  endsel

  cls_ yellow,black,red
  paper #ow,red
  print #ow,\"SOLICITUD DE PRESTAMO EXTRANJERO"
  center #ow,12,"ESPERA"
  pause 50
  if country=usa
    tune "2m1j3f3j3m4r1 2v1t3r3j3l4m"
  else
    tune "2g2d3i4d2 2g2d3i4d"
  endif
  at #ow,12,0
  if country=usa
    print #ow,"Los americanos";
  else
    print #ow,"Los rusos";
  endif

  if months<int(rnd*5)+3
    at #ow,12,2:print #ow,"opinan que es demasiado pronto para dar"\"ayudas economicas"
  else
    if decision$(decision,1)="*"
      at #ow,12,2:print #ow,"te deniegan un nuevo prestamo"
    else
      if group$(country,popularity)<=low
        at #ow,12,12
        print #ow,'te dicen que no, sin ninguna explicacion.'
      else
        print #ow," te concederan"
        let loan=group$(7+x,popularity)*30+rnd(0 to 200)
        at #ow,14,7:print #ow,y;",000 dolares"
        let money=money+loan
        let decision$(38+x,1)="*"
      endif
    endif
  endif
  wait_key_press

enddef

defproc decision

  loc a,c,chosen_decision,options

  rep choose_gratis_decision
    rep choose_decision

      cls_ red,yellow,blue

      print #ow,fill$("*",columns*ow_lines)
      paper #ow,blue:ink #ow,white
      center #ow,3,"DECISION PRESIDENCIAL"
      paper #ow,yellow:ink #ow,black
      at #ow,6,1:print #ow,"Elige entre:"
      at #ow,08,4:print #ow,"1. Complacer a un grupo  "
      at #ow,10,4:print #ow,"2. Complacer a todos     "
      at #ow,12,4:print #ow,"3. Tus negocios          "
      at #ow,14,4:print #ow,"4. Aumentar el tesoro    "
      at #ow,16,4:print #ow,"5. Fortalecer a un grupo "

      let k$=get_key$: cls #ow
      sel on code(k$)
        =code("1"):let x$="-2"
        =code("2"):let x$="35"
        =code("3"):let x$="69"
        =code("4"):let x$=":<"
        =code("5"):let x$="=?"
        =remainder:ret
      endsel

      at #ow,(20-(((code(x$(2))-20)-(code(x$(1))-20))*3))*.5,0
      let options=0
      for a=code(x$(1))-20 to code(x$(2))-20
        if decision$(a,1)<>"*"
          let options=options+1
          print #ow,options;"."
          print #ow,decision$(a,18 to )
        endif
      endfor a

      if not options
        at #ow,12,3
        print #ow,"Esta seccion esta agotada"
        pause 150
        next choose_decision
      endif

      let k$=get_key$

      if not k$ instr "0123456"
        next choose_decision
      endif
      if k$<1 or k$>(code(x$(2))-20)+1-(code(x$)-20)
        next choose_decision
      endif
      let chosen_decision=code(x$)-20+k$-1
      if decision$(chosen_decision,1)="*"
        next choose_decision
      endif

      sel on chosen_decision
        =37
          money_transfer
          exit choose_decision
        =38,39
          ask_for_loan chosen_decision
          treasure_report
          exit choose_gratis_decision
      endsel

      advert chosen_decision
      rem restore the screen colors here?!!!
      at #ow,4,0
      print #ow,decision$(chosen_decision,18 to )
      let c=cash_advice(chosen_decision)
      if not c
        pause 200
        next choose_decision
      endif
      at #ow,4,0
      print #ow,decision$(chosen_decision,18 to )
      if not yes_key:next choose_decision

      if chosen_decision<>35
        treasure_report
        take_only_once_decision chosen_decision
        exit choose_decision
      endif

      let strength=strength+2
      treasure_report
      take_decision chosen_decision
      exit choose_decision

    endrep choose_decision
    treasure_report_details_2
    exit choose_gratis_decision
  endrep choose_gratis_decision

enddef

defproc news

  loc a,event,first_event,last_event

  let first_event=44
  let last_event=49
  if rnd(0 to 2):ret

  let event=rnd(first_event to last_event)
  for a=1 to 6
    if not (decision$(event,1)="N"):exit a
    let event=event+1
    if event>last_event:let event=first_event
  next a
    ret
  endfor a

  cls_ white,black,blue
  at #ow,10,11:print #ow,"noticias"
  for a=1 to 10: zx_beep .6,30
  cls #ow
  at #ow,10,11:print #ow,"noticias"
  at #ow,14,0:print #ow,decision$(event,18 to )
  ink #ow,white
  pause 100
  take_only_once_decision event: plot
  police_report

enddef

defproc money_transfer

  loc amount

  cls #ow
  at #ow,3,0
  print #ow,"TRANSFERENCIA A LA CUENTA SUIZA"\\\

  let amount=int(money/2)
  if amount>=1
    print #ow,"El tesoro tenia $";int(money);",000"
    let money_in_switzerland=money_in_switzerland+amount
    let money=money-amount
    pause 100
    print #ow,\\"$";amount;",000 han sido transferidos"
    exit choose_decision
  else
    center #ow,12,"Ninguna transferencia hecha"
    pause 100
  endif

enddef

defproc the_end

  loc a,x

  if alive
    pause 100
  else
    pause 50
    tune "4d3d1d3d3g1f2f1d2d1d5d"
  endif

  let x=0
  for a=1 to 8: let x=x+group$(a,popularity)
  cls_ yellow,black,cyan
  at #ow,3,1:print #ow,"Tu puntuacion como presidente"
  print #ow,\" popularidad final - ";to 28;x
  print #ow,\" meses en el poder (";months;"x3) - ";to 28;months*3: let x=x+months*3

  if alive
    print #ow,\" Por estar vivo - ";to 28;alive
    print #ow,\" Por el ahorro"\"     ($";money_in_switzerland;",000 /10,000) -";to 28;int(money_in_switzerland/10)
    let x=x+alive+int(money_in_switzerland/10)
  endif

  print #ow,\\\" Tu total es ";to 28;:print #ow,x
  if x>score:let score=x
  print #ow,\"[ La mayor puntuacion es ";score;" ]"
  wait_key_press: cls #ow
  cls_ black,white,black
  at #ow,1,14:print #ow,"final"
  police_report_step_3: wait_key_press
enddef

defproc national_flag
  loc a,height,x,y
  let height=11
  let x=center_for(18)
  let y=8
  for a=y to y+height
    at #ow,a,x
    paper #ow,red
    print #ow,"   ";
    paper #ow,blue
    print #ow,"            ";
    paper #ow,green
    print #ow,"   "
  endfor a
  paper #ow,blue:ink #ow,yellow
  at #ow,13,13:print #ow,"******"
  at #ow,14,13:print #ow,"  **  "
  at #ow,15,13:print #ow,"******"
enddef

defproc national_slogan(text$)
  loc a,times
  let times=columns*ow_lines/len(text$)
  for a=1 to times
    print #ow,text$;" ";
    zx_beep .01,40-a+rnd*10
  endfor a
enddef

defproc title
  loc a
  cls_ cyan,black,black
  national_slogan "Ritimba"
  national_flag
  for a=1 to 50: zx_beep .03,a
  paper #ow,white:ink #ow,black
  center #ow,3," Pulsa una tecla y gobernaras "
  center #ow,5," Ritimba "
  national_anthem
enddef

defproc national_anthem
  loc a,pitch,tune$
  let tune$="KPKKMKIHK`KMRPOMOP"
  cursen_home #iw
  rep listen
    for a=1 to len(tune$)
      if len(inkey$(#iw)):exit listen
      let pitch=code(tune$(a))-80
      if pitch=16
        rem pause 20:rem this makes INKEY$ to fail in QPC2!!!
      else
        zx_beep .5,pitch
      endif
    endfor a
    rem pause 30:rem this makes INKEY$ to fail in QPC2!!!
  endrep listen
  curdis #iw
enddef

defproc war
  loc a
  if group$(leftoto,popularity)>low:ret
  if group$(leftoto,power)<low:ret
  if not rnd(0 to 3)
    actual_war
  else
    cls_ black,white,cyan:
    at #ow,6,1:print #ow,"Tratado de guerra con Leftoto"
    at #ow,10,3:print #ow,"Tu popularidad en Ritimba"
    at #ow,12,11: print #ow,"aumentara"
    for a=1 to 3: peace_pleases_this_group a
    peace_pleases_this_group 6
  endif
enddef

defproc peace_pleases_this_group(group):rem rename!!!
  if group$(group,popularity)<"9"
    let group$(group,popularity)=group$(group,popularity)+1
  endif
enddef

defproc actual_war

  cls_ red,black,black
  center #ow,8,"Leftoto nos invade"
  let rs=0
  for a=1 to 3
    if group$(a,popularity)>low:let rs=rs+group$(a,power)
  endfor a
  if group$(police,popularity)>low
    let rs=rs+group$(police,power)
  endif
  let rs=rs+strength
  at #ow,12,1:print #ow,"La fuerza de Ritimba es ";rs
  let ls=0
  for a=1 to 6
    if group$(a,popularity)<=low:let ls=ls+group$(a,power)
  endfor a
  at #ow,14,1:print #ow,"La fuerza de Leftoto es ";ls

  at #ow,18,3:print #ow,"Una corta y decisiva guerra"
  fx_war

  if ls+rnd(-1 to 1)<rs

    rem Ritimba wins

    zx_border black
    cls #ow
    at #ow,12,7
    print #ow," Leftoto derrotado ": 
    let group$(leftoto,power)="0"

  else

    rem Leftoto wins

    cls_ black,white,black
    at #ow,7,7
    print #ow,"Victoria de Leftoto"

    if not(decision$(36,1)="*" and rnd(0 to 2))

      let alive=0
      if decision$(36,1)="*"
        at #ow,10,0
        print #ow,"El motor del helicoptero se para"
        pause 80
      endif
      at #ow,12,4
      print #ow,"Eres acusado de ser un"\\"  enemigo del pueblo y..."
      pause 30: fx_2
      at #ow,18,7:print #ow,"Sumarisimamente ejecutado"
    else
      rem Escape
      cls #ow 
      at #ow,12,3:print #ow,"escapas en helicoptero!"
      let escape=1

    endif

  endif

enddef

defproc key_prompt(prompt$,colour)
  paper #iw,colour
  ink #iw,contrast_colour(colour)
  cls #iw
  center #iw,1,prompt$
  cursen_home #iw
enddef

deffn get_key_prompt$(prompt$)

  rep dont_press_now
    if inkey$(#iw)="":exit dont_press_now
  endrep dont_press_now

  rep press_now

    key_prompt prompt$,prompt_colour_1
    let key$=inkey$(#iw,50)
    if key$<>"":exit press_now
    zx_beep .01,30

    key_prompt prompt$,prompt_colour_2
    let key$=inkey$(#iw,50)
    if key$<>"":exit press_now
    zx_beep .01,20

  endrep press_now
  curdis #iw

  ret key$

enddef

deffn get_key$
  ret get_key_prompt$("TECLA")
enddef

deffn yes_key
  loc yes
  let yes = "s"==get_key_prompt$('TECLA ("S" = SI)')
  zx_beep .25,10+40*yes
  ret yes
enddef

defproc wait_key_press
  loc key$
  let key$=get_key$
enddef

rem chars:
rem 01: decision already taken ("N"=no, "*"=yes)
rem 04-11: +/- popularity for groups 1-8 (..."K"=-1, "L"=-1,"M"=0, "N"=1...)
rem 12-17 +/- power for groups 1-6 (..."K"=-1, "L"=-1,"M"=0, "N"=1...)
rem 18...:   decision or event
DATA "NMHQJLMMMMMPKLMMMPONER MILI OBLIGATORIA          "
DATA "NMMPMJMMMMMNMLMMMREQUISAR TIERRAS PARA TIRO      "
DATA "NCMPLNMLMLMNMNIMMATACAR LAS BASES DE LA GUERILLA "
DATA "NEMPLMMIMLMNMNKMMATACAR BASE GUERRILLA EN LEFTOTO"
DATA "NMMQONMMIMMNMNMMJECHAR AL JEFE DE LA POL.SEC.    "
DATA "NMMPMMMLMIOMMMMMMECHAR A LOS MILITARES RUSOS     "
DATA "NMDQMLMMMMMOLLLMMAUMENTAR LA PAGA DE LAS TROPAS  "
DATA "NAMQLLMLLMMPLLKLMCOMPRAR MAS ARMAS Y MUNICIOMES  "
DATA "NMMLONMMMMMLMMLMMPARAR LOS ABUSOS DEL EJERCITO   "
DATA "NMMMQIMNMMMMOLMMMAUMENTAR EL SALARIO MINIMO      "
DATA "NMPNQOMMIMMNNNNMJACABAR CON LA POLICIA SECRETA   "
DATA "NMMMPKMKMMMMOKMMMPARAR LA INMIGRACION DE LEFTOTO "
DATA "NCELQKMOLNMMNLLMMPONER ESCUELA GRATIS PARA TODOS "
DATA "NMMMQJMNLNMMPJMMLLEGALIZAR LOS SINDICATOS        "
DATA "NMMLQKMNLMMMOLLMMLIBERAR A SU LIDER ENCARCELADO  "
DATA "NMSMPLMMMMMMMMLMMINICIAR UNA LOTERIA PUBLICA     "
DATA "NMMKMPMMMMMLMMMMMPARAR USO MILITAR DE SUS TIERRAS"
DATA "NMMMIQMLMLMMKONMMBAJAR EL SALARIO MINIMO         "
DATA "NWHMMPMNMOIMMNMMMNACIONALIZAR NEGOCIOS AMERICANOS"
DATA "NMRMMPMJMLMMNOMLMTASAR IMPORTACIONES DE LEFTOTO  "
DATA "NMQNNPMMIMMNMNNMKCORTAR LOS GASTOS DE LA POL.SEC."
DATA "NMHMMQMMMMMMMOMMMBAJAR IMPUESTO SOBRE LA TIERRA  "
DATA "NMMKLPMMMMMLLNNMMCEDER TROPA PARA LABRAR TIERRA  "
DATA "NACNNPMJMONMMPMKMCONSTRUIR UN SISTEMA DE RIEGO   "
DATA "NMMQLLMMLMMNMMLMLHACER MINISTRO AL JEFE-EJERCITO "
DATA "NLILQNMOMNMMMMLMMHACER CLINICAS PARA OBREROS     "
DATA "NMMLKQMMLLMLLOMMLDAR PODERES A LOS TERRATENIENTES"
DATA "NRMKMMMQMKNLMMLPMVENDER ARMAS U.S.A. A LEFTOTO   "
DATA "NYMMMLMLMKPMMMMMMVENDER DERECHOS A FIRMAS U.S.A. "
DATA "NMWKMMMMMPJMMMMNMALQUILAR A RUSIA UNA BASE NAVAL "
DATA "NMENPPMMMMMLMMLMMBAJAR LOS IMPUESTOS             "
DATA "NEMPPPMMMMMMMMLMMHACERTE CAMPA#A DE POPULARIDAD  "
DATA "NMUPPPMMDMMONNNMDQUITAR PODERES A LA POL.SEC.    "
DATA "NMGJJJMMUMMLLLLMUAUMENTAR PODERES A LA POL.SEC.  "
DATA "NIMKLLMMLMMKMMMMLAUMENTAR TUS GUARDAESPALDAS    *"
DATA "NAMIIJMMKMMMMMMMMCOMPRAR UN ESCAPE EN HELICOPTERO"
DATA "NMMMMMMMMMMMMMMMMTRANSFERENCIA A UN BANCO SUIZO *"
DATA "NMMMMMMMMMMMMMMMMPEDIR A LOS RUSOS UNA AYUDA     "
DATA "NMMMMMMMMMMMMMMMMPEDIR A LOS AMERICANOS UNA AYUDA"
DATA "NZMNNPMGMKMMMMMMMNACIONALIZAR NEGOCIOS DE LEFTOTO"
DATA "NHMPMMMJMLMRMMKKLCOMPRAR ARMAS PARA EL EJERCITO  "
DATA "NMMMPLMMLMMMRLPMLPERMITIR ASOCIACIONES CAMPESINAS"
DATA "NMMLLPMMLMMLLRLMLDAR A TERRATEN. EJERCITO PRIVADO"
DATA "NMMMMMMMIMMMMMQMIARCHIVOS DE LA POL.SEC. ROBADOS!"
DATA "NMMMMMMMMMMLMMVMMCUBA ENTRENA A LAS GUERILLAS    "
DATA "NMMMMMMMMMMIMMOMNBARRACON DEL EJERCITO HA VOLADO "
DATA "NMMMMMMMMMMMMJMKMPRECIO DE PLATANOS CAE AL 98%   "
DATA "NMMMMMMMMMMMMOMIMCORONEL GOMEZ SE FUGA A LEFTOTO "
DATA "NMMMMMMMMMMMILKMMEPIDEMIA ENTRE LOS CAMPESINOS   "
data "76::$el ejercito"
data "76::'los campesinos"
data "76::+los terratenientes"
data "06--&las guerillas"
data "76--%los leftotos"
data "76--+la policia secreta"
data "7---#los rusos"
data "7---'los americanos"
data "ejercito  ","campesinos","terraten. ","guerillas "
data "leftotos  ","pol.sec.  ","rusos     ","americanos"

defproc s
rem save "dos1_sb_ritimba_ritimba_bas"
enddef

rem _________________________________________________
rem New for SuperBASIC

defproc zx_border(colour):rem tmp!!!
enddef
defproc zx_beep(duration,pitch)
enddef

deffn center_for(width_in_chars)
  ret (columns-width_in_chars)/2
enddef

defproc center(channel,line_,text$)
  at #channel,line_,center_for(len(text$))
  print #channel,text$
enddef 

deffn csize_width_pixels(w)
  sel on w
    =0:ret 6
    =1:ret 8
    =2:ret 12
    =3:ret 16
  endsel
enddef

deffn csize_height_pixels(h)
  ret h*10+10
enddef

deffn contrast_colour(colour)
  rem todo!!! make it with a bit mask
  sel on colour
    =black,blue,red,purple:ret white
    =remainder:ret black
  endsel
enddef

defproc cursen_home(channel)
  cursen #channel
  if channel=iw:at# iw,iw_lines-1,columns-1:else at #ow,ow_lines-1,columns-1
enddef

deffn ow_line_y(line_)
  rem Return the y pixel coord of the given line in #ow.
  ret ow_char_height_pixels*line_
enddef

deffn ow_column_x(column)
  rem Return the x pixel coord of the given column in #ow.
  ret ow_char_width_pixels*column
enddef

defproc init_windows

  let columns=32
  let lines=24

  let iw_lines=3:rem input window lines
  let ow_lines=lines-iw_lines:rem output window lines

  let ow=fopen("scr_"):rem output window
  let ow_csize_width=3
  let ow_csize_height=1
  csize #ow,ow_csize_width,ow_csize_height
  let ow_char_width_pixels=csize_width_pixels(ow_csize_width)
  let ow_char_height_pixels=csize_height_pixels(ow_csize_height)
  window #ow,columns*ow_char_width_pixels,ow_lines*ow_char_height_pixels,0,0

  let iw=fopen("con_"):rem input window
  csize #iw,3,1
  window #iw,columns*16,iw_lines*20,0,(lines-iw_lines)*20

  let blank_line$=fill$(" ",32)

enddef

defproc init_screen
  mode 8
  rem colour_ql
  let black=0:let blue=1:let red=2:let purple=3
  let green=4:let cyan=5:let yellow=6: let white=7
  if ver$="HBA"
    let scr_width=scr_xlim:let scr_height=scr_ylim
  else
    let scr_width=512:let scr_height=256
  endif
enddef

defproc init_once
  tk2_ext
  init_screen
  init_windows
enddef


ritimba
