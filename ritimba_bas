   1 rem 2011-03-26 código de Dictator traducido por mí, sin tocar todavía para adaptarlo a SBASIC
   2 rem 2011-04-01 Primeros y muchos cambios en el código. Hay zonas en que el flujo no está claro y no será fácil dividir el código en procedimientos.

   3 defproc credits
   4 cls_ black,white,blue: at #ow,1,0:print #ow,"RITIMBA"\\"Por:"\"Marcos Cruz (programandala.net),"\"2011"
   5 print #ow,\\"Una version en SuperBASIC de..."
   6 print #ow,"DICTADOR, de Don priestley, 1983"
   9 enddef

  10 defproc fx_war
  11 enddef
  
  70 defproc fx_2
  71 enddef

  79 rem _________________________________________________
  80 init

  82 credits
  83 let score=0
  84 let b$="123456789"
  85 let c$="987654321"
  86 let decisions=49
  87 let groups=8
 110 dim decision$(decisions,49)
 111 dim group$(groups,23)
 112 restore
 116 for a=1 to decisions: read decision$(a)
 120 get_key:title

 130 rem cycle _________________________________________________
 140 rem restore 6150:rem needed?!!!
 141 for a=1 to groups: read group$(a)
 200 for a=1 to decisions: let decision$(a,1)="N"
 202 let money=1e3
 203 let escape=0
 204 let month_payment=60
 206 let strength=4
 208 let money_in_switzerland=0
 210 let alive=10
 212 let months=0
 222 let pc=0
 238 let dec=2500
 244 let revolution_strength=10
 250 cls_ white,black,blue
 252 paper #ow,cyan:flash #ow,1:center #ow,1,"Bienvenido al cargo"
 253 paper #ow,7
 254 print #ow,"El anterior dictador de nuestra"
 256 print #ow,"amada patria Ritimba tuvo una"
 258 print #ow,"puntuacion final de ";score
 260 if score<=0
 261 print #ow,\\"Como este es tu primer intento"\"no dudes en hacerlo mejor!"
 262 else
 264 print #ow,\"Intenta al menos acabar con ";score+1;"!"
 265 endif
 266 print #ow,\\"Comienza con un informe del"
 267 print #ow,"tesoro y con otro de la policia."
 270 get_key
 272 treasure_report
 273 get_key
 274 police_report_step_2

 600 rem audiencias ____________________________________________
 601 let c=1
 603 let low=int(rnd*3)+2
 604 let revolution_strength=int(rnd*3)+10
 610 let months=months+1
 612 cls_ yellow,black,blue
 613 at #ow,10,12:paper #ow,cyan:ink #ow,white:print #ow,"mes  ":paper #ow,white: zx_bright #ow,1: print #ow,months
 614 pause 50
 616 plot
 618 if money<0:bankruptcy
 620 if money>0:let money=money-month_payment:rem logic!!! "else" would work better!!
 622 cls yellow,black,yellow
 624 at #ow,0,0:paper #ow,green:print #ow,blank_line$;blank_line$;blank_line$;blank_line$;blank_line$
 625 at #ow,3,9:zx_bright #ow,1: zx_inverse #ow,1:print #ow,"una audiencia"
 630 let r=int(rnd*24)+1
 632 for a=1 to 22: if decision$(r,1)="N":goto 648
 634 let r=(r-int(r/24)*24)+1: endfor a
 640 for a=1 to 24: let decision$(a,1)="N": endfor a: goto 630
 648 let decision$(r,1)="*"
 650 let gs=int((r-1)/8)+1
 651 zx_border gs
 652 let t$=group$(gs,6 to code(group$(gs,5))-20)
 654 let r$=decision$(r)
 655 rem gosub 410+gs:rem graphics removed!!!
 656 at #ow,10,1:print #ow,"Peticion de ";t$
 662 at #ow,14,1:print #ow,"Esta su excelencia conforme en"
 663 at #ow,16,0:zx_bright #ow,1:print #ow,r$(18 to )
 666 get_key: zx_border 0: advert

 670 rem opcion ________________________________________________
 684 cls #ow: at #ow,1,10:zx_bright #ow,1:flash #ow,1:print #ow,"  decision  "
 688 print #ow,\to (32-(12+len(t$)))/2:paper #ow,gs:print #ow,"peticion de ";t$
 690 at #ow,5,0:zx_bright #ow,1:print #ow,r$(18 to ):zx_bright #ow,0:paper #ow,blue:print #ow,\\blank_line$
 694 if not c:goto 730: let a=0
 696 cash_advice: print #ow,: ink #ow,green:print #ow,blank_line$
 698 if c=0:goto 730
 722 get_key_yes
 726 if k$="s":goto 760
 728 goto 740
 730 cls #ow : at #ow,10,1:print #ow,"No tiene ";:zx_bright #ow,1:print #ow,"suficientes fondos":zx_bright #ow,0:print #ow,\\" para";
 731 print #ow," pagar esta decision."
 732 at #ow,15,4:print #ow,"Su respuesta debe ser ";:flash #ow,1:print #ow,"no": pause 250
 734 let c=1
 740 let x=group$(gs,1)
 744 let y=code(r$(gs+3))-77
 746 let x=x-y
 748 if x<0:let x=0
 750 let group$(gs,1)=x

 751 cls #ow
 752 treasure_report: goto 766
 760 treasure_report: money_transfer: treasure_report_details
 766 plot: get_key
 775 murder: if alive=0:the_end
 778 war: if alive=0 or escape=1:the_end
 779 plot
 780 police_report: goto dec
 781 rem!!!

 790 defproc treasure_report
 792 cls_ white,black,green
 794 for a=0 to 20: ink #ow,green:print #ow,"$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$": endfor a
 795 treasure_report_details
 796 enddef

 797 defproc treasure_report_details
 798 at #ow,8,7:zx_bright #ow,1:print #ow,"informe del tesoro"
 799 treasure_report_details_2
 823 enddef

 824 defproc treasure_report_details_2
 825 paper #ow,blue: ink #ow,white
 826 zx_bright #ow,1: at #ow,12,1:print #ow," el tesoro";
 828 if int(money)>=0:print #ow," tiene $";
 830 if money<0:print #ow," ";:flash #ow,1:print #ow,"debe";:flash #ow,0:print #ow," $";
 831 print #ow,abs(int(money));" 000 "
 832 at #ow,14,1:print #ow," gastos del mes son $";month_payment;",000 "
 838 if money_in_switzerland>0:at #ow,17,2:print #ow,"[En suiza tiene $";money_in_switzerland;",000]"
 842 zx_bright #ow,0
 843 enddef

 900 defproc bankruptcy
 910 cls #ow
 914 at #ow,5,2:print #ow,"El tesoro esta en ";:flash #ow,1:print #ow,"bancarrota"
 918 at #ow,9,0:print #ow,"Su popularidad con el ejercito y"\\
 920 print #ow,"con la policia secreta caera!"\\\
 922 print #ow,"el poder de la policia bajara"\\
 924 print #ow,"y tu propio poder tambien"
 930 if group$(1,1)>"0":let group$(1,1)=group$(1,1)-1
 932 if group$(6,1)>"0":let group$(6,1)=group$(6,1)-1
 933 if group$(6,2)>"0":let group$(6,2)=group$(6,2)-1
 934 if strength>0:let strength=strength-0
 940 pause 250: plot: police_report
 944 enddef

1000 defproc advert
1002 cls_ green,black,blue
1004 for a=0 to 19: at #ow,a,11: flash #ow,1: paper #ow,cyan:print #ow,"? aviso ?"
1010 get_key_yes: cls_ yellow
1014 if k$<>"s":ret
1022 at #ow,1,0:zx_inverse #ow,1:print #ow,decision$(r,18 to ): print
1026 at #ow,3,0:print #ow,"Su popularidad con...": print
1027 for a=1 to 8
1028 let x=code(decision$(r,a+3))-77: if x=0:next a:exit a
1029 print #ow,to 2;group$(a,6 to );to 21;
1031 if x>0:print #ow,"+";
1032 print #ow,x;
1033 if gs=a and r<25:zx_inverse #ow,1:ink #ow,gs:print #ow,"< "
1034 endfor a
1036 print #ow,\\\"El poder de...": print
1038 for a=1 to 6
1039 let x=code(decision$(r,a+11))-77: if x=0:next a:exit a
1041 print #ow,to 2;group$(a,6 to );to 21;
1042 if x>0:print #ow,"+";
1044 print #ow,x
1048 endfor a
1050 get_key: cls #ow
1051 enddef

1300 rem cls __________________________________________________
1305 defproc cls_(paper_color,ink_color,border_color)
1306 paper #ow,paper_color
1307 ink #ow,ink_color
1310 zx_border border_color: cls #ow
1311 paper #iw,border_color: cls #iw
1313 zx_beep .1,40
1315 enddef

1400 defproc plot

1410 if months<=2:ret
1420 for a=1 to 3: let group$(a,3 to 4)="::"
1426 if months<pc:ret
1430 for a=1 to 3
1432 if group$(a,1)>low:next a:exit a:rem check!!! goto 1450
1436 for p=1 to 6
1438 if a=p or group$(p,1)>low:next p:exit p:rem check the logic!!! goto 1441
1440 if group$(p,2)+group$(a,2)>=revolution_strength:exit p:rem check the logic!!! goto 1446
1441 next p
1442 let group$(a,3)="A":next a:exit a:rem check the logic!!! goto 1450
1443 endfor p:rem new!!! but not goot
1446 let group$(a,3)="R"
1448 let group$(a,4)=p
1450 endfor a
1452 enddef


1500 defproc murder

1510 let r=int(rnd*3)+1
1514 if group$(r,3)<>"A":ret

1516 cls_ black,white,black: at #ow,10,7:print #ow,"intento de asesinato"
1518 at #ow,20,0:print #ow,"por uno de ";group$(r,6 to )
1520 rem for a=60 to 1 step -.5: poke 23886,a: let i=usr 23884: endfor a: poke 23886,70:old!!!
1522 cls #ow : pause 30: fx_2: pause 50

1526 if (group$(1,3)="A" and group$(2,3)="A" and group$(3,3)="A") or not(group$(6,1)>low or group$(6,2)>low or int((rnd*2))):rem check the compacted logic!!!
1546 cls_ black,white,black: at #ow,12,6:print #ow,"Esta usted muerto!"
1548 zx_beep 3,-40
1550 let alive=0: ret
1551 else

1554 cls_ white,black,black: at #ow,12,8:flash #ow,1: zx_bright #ow,1:print #ow," Intento fallido "
1556 rem poke 23886,50: let a=usr 23842:rem old!!!
1557 endif
1560 enddef

1570 defproc tune(m$)
1572 for a=1 to len(m$) step 2
1574 if m$(a+1)=" ":pause code(m$(a))/4:next a:exit a
1576 zx_beep (code(m$(a))-48)/6,code(m$(a+1))-105
1578 endfor a
1580 enddef

1600 rem transferencia _________________________________________
1605 defproc money_transfer
1610 let decision$(r,1)="*"
1611 money_transfer_
1612 enddef
1619 defproc money_transfer_:rem rename!!!
1620 let t$=decision$(r,4 to 11)
1621 for a=1 to 8
1622 if t$(a)<>"M":rem maybe "M"
1624 let x=group$(a,1)+(code(t$(a))-77)
1626 if x>9:let x=9
1628 if x<0:let x=0
1630 let group$(a,1)=x
1631 endif
1632 endfor a
1638 let t$=decision$(r,12 to 17)
1640 for a=1 to 6
1642 if t$(a)="M":next a:exit a
1644 let x=group$(a,2)+(code(t$(a))-77)
1646 if x>9:let x=9
1648 if x<0:let x=0
1650 let group$(a,2)=x
1652 endfor a
1660 let money=money+cst
1662 let month_payment=month_payment-mcst
1664 if month_payment<0:let month_payment=0
1666 enddef

1700 defproc police_report

1702 cls_ black,white,black
1709 at #ow,6,1:zx_bright #ow,1:print #ow,"informe de la policia secreta";
1710 if money<=0 or group$(6,1)<=low or group$(6,2)<=low:goto 1764
1713 flash #ow,1:print #ow,"?": at #ow,12,8:print #ow,"( cuesta $1000 )"
1714 get_key_yes:if k$<>"s":ret
1715 let money=money-1
1716 police_report_step_2
1717 enddef

1718 defproc police_report_step_2
1719 cls_ black,white,black:print #ow,"mes ";months:at #ow,3,0: ink #ow,blue: flash #ow,1:print #ow,blank_line$:police_report_step_3
1720 enddef

1721 defproc police_report_step_3
1722 at #ow,3,8:zx_inverse #ow,1:zx_bright #ow,1:print #ow,"informe policial"
1723 rem at #ow,5,14:zx_inverse #ow,1:print #ow,"\j\k\l";at #ow,6,14;"\m\n\o":rem graphics!!!
1724 at #ow,6,1:print #ow,"popularidad":at #ow,6,21:print #ow,"poder"
1725 restore 6200: for a=1 to 8
1726 read k$:at #ow,7+a,11: paper #ow,yellow: flash #ow,0+(group$(a,3)="R"):print #ow,k$
1728 at #ow,7+a,10: paper #ow,white: zx_bright #ow,1:print #ow,a
1729 if a<=3 and group$(a,3)="R":at #ow,7+a,21: paper #ow,white:print #ow,group$(a,4)
1732 let x=group$(a,1): at #ow,7+a,10-x: ink #ow,white: paper #ow,green:print #ow,c$(10-x to )
1734 if a<=3 and group$(a,3)="A":at #ow,a+7,21: paper #ow,white: flash #ow,1:print #ow,"A"
1736 endfor a
1738 for a=1 to 6: at #ow,7+a,22: paper #ow,red: ink #ow,white:print #ow,b$( to group$(a,2))
1742 at #ow,17,1:print #ow,"Tu poder es ";strength
1744 at #ow,19,1:print #ow,"fuerza para una revolucion :";revolution_strength
1750 get_key
1751 cls_ green,black,green
1752 ret

1764 at #ow,10,9:print #ow,"no disponible"\\
1766 if group$(6,1)<=low:print #ow,"Tu popularidad con nosotros es ";group$(6,1)\\
1768 if group$(6,2)<=low:print #ow,to 3;"El poder de la policia es ";group$(6,2)\\
1769 if money<=0:print #ow,to 3;"No puedes obtener un informe"
1770 rem poke 23886,30: for a=1 to 80: let i=usr 23884: endfor a:rem old!!!
1772 rem poke 23886,50

1773 enddef

1800 defproc revolution
1801 news
1802 for a=1 to 3: let r=int(rnd*3)+1
1804 if group$(r,3)="R":goto 1810
1806 endfor a
1807 goto 600
1810 flash #ow,1: cls_ red,black,red
1811 ink #ow,white: at #ow,10,11:flash #ow,0: zx_bright #ow,0:print #ow,"revolucion"
1812 for a=1 to 5: zx_beep .5,20: zx_beep .5,10: endfor a
1814 flash #ow,0: cls_ yellow,black,yellow
1816 at #ow,12,8:print #ow,"intento de escape?": get_key_yes
1830 cls #ow : if k$<>"s":goto 1850
1832 if decision$(36,1)="*":goto 1842
1833 at #ow,10,2:print #ow,"Tienes que atravesar el";:at #ow,12,6:print #ow,"monte hacia leftoto"
1834 pause 200: cls #ow

1836 if int((rnd*((group$(4,2)/3)+.4))):goto 1839
1837 at #ow,12,0: flash #ow,1: zx_bright #ow,1:print #ow,"  Las guerillas no te cojieron  ": rem let i=usr 23842:rem old!!!
1838 the_end

1839 cls_ black,white,black
1840 let alive=0: pause 50: fx_2 : at #ow,12,0:print #ow,"Las guerillas estan ";:flash #ow,1: zx_bright #ow,1:print #ow,"celebrandolo": fx_2: the_end

1842 if int(rnd*3)
1843 at #ow,12,4:flash #ow,1: zx_bright #ow,1:print #ow,"escapas en helicoptero!": the_end: rem let i=usr 23842:rem old!!!
1844 endif

1845 at #ow,10,2:print #ow,"el helicoptero no funciona!"
1846 pause 150
1847 cls #ow : goto 1833
1850 let x=group$(r,2)+group$(group$(r,4),2)
1852 at #ow,5,0:print #ow,group$(r,6 to code(group$(r,5))-20);" se ha unido a "
1856 print #ow,group$(group$(r,4),6 to )\\
1857 print #ow,"Su fuerza unidos es ";x
1858 print #ow,\\"a quien vas a pedir ayuda?"
1860 let z=1
1861 for a=1 to 6
1862 if group$(a,1)<=low:next a:exit a
1864 print #ow,to 6;a;" .";group$(a,6 to code(group$(a,5))-20);" ?"
1866 let z=0
1868 endfor a

1869 if z:goto 1890
1870 get_key: if k$<"1" or k$>"6":goto 1870
1878 if group$(k$,1)<=low:goto 1884
1880 let h=k$: goto 1893
1884 cls #ow : at #ow,12,5:print #ow,"Debes estar bromeando!": goto 1846

1890 cls #ow : at #ow,8,5:print #ow,"estas solo!"
1892 get_key

1893 cls #ow
1894 at #ow,8,0:print #ow,"Tu fuerza es ";strength
1895 print #ow,\\"La fuerza de ";group$(h,6 to code(group$(h,5))-20);" es ";group$(h,2)
1896 print #ow,\\"La de los revolucionarios es ";x
1898 pause 250
1900 cls_ white,black,white: at #ow,12,3:flash #ow,1: zx_bright #ow,1:print #ow,"La revolucion ha comenzado"
1904 fx_war
1914 if x<=strength+group$(h,2)+int(rnd*3)-1:goto 1950
1922 cls_ black,white,black: at #ow,10,7:print #ow,"Has sido derrocado"
1923 at #ow,12,10:print #ow,"y ";: flash #ow,1:print #ow,"liquidado"
1924 fx_2
1926 let alive=0: pause 50: the_end
1950 cls_ black,white,black : at #ow,10,2:zx_bright #ow,1:flash #ow,1:print #ow,"La revuelta ha sido sofocada"
1952 rem let a=usr 23842
1954 at #ow,0,0: for a=1 to 22: paper #ow,int(rnd*5)+1:print #ow,blank_line$
1955 at #ow,10,0: zx_bright #ow,1:print #ow,"castigas a los revolucionarios? "
1956 get_key_yes: if k$<>"s":goto 1968
1960 for n=1 to 3: fx_2: let y=sqrt(1): endfor n
1964 let group$(r,1 to 2)="00"
1966 let group$(group$(r,4),1 to 2)="00"
1968 let group$(h,2)="9"
1969 let pc=months+2: plot: police_report: goto 600:rem goto old!!!
1970 enddef

1980 defproc cash_advice

1990 let cst=10*(code(decision$(r,2))-77)
1992 let mcst=code(decision$(r,3))-77
1994 if not cst and not mcst:let c=1: at #ow,10,7:print #ow,"no cuesta dinero": ret

1996 at #ow,9,1:print #ow,"Esta decision ": print

1998 if not cst:goto 2014

2002 if cst>0:print #ow," daria al";
2004 if cst<0:print #ow," costaria al";
2008 print #ow,"  tesoro ";abs(cst);" 000 $": print
2010 if not mcst:goto 2020

2012 print #ow," y": print

2014 if mcst<0:print #ow," subiria";
2016 if mcst>0:print #ow," bajaria";
2018 print #ow," los costos mensuales en ";abs(mcst);" 000 $"

2020 if money+cst>0:let c=1: ret
2021 if (cst<0 or mcst<0) and (money+cst<0 or money+mcst<0):goto 2034
2022 let c=1: ret

2034 pause 250: let c=0: cls #ow
2037 at #ow,5,0:print #ow,decision$(r,18 to )
2039 at #ow,8,2:print #ow,"El dinero necesario"
2040 print #ow,\to 4;"no esta en el tesoro"\\\
2042 if decision$(38,1)="N":print #ow,"Quizas los rusos pueden ayudar?": print
2044 if decision$(39,1)="N":print #ow,"Los americanos son un pueblo"\"generoso": print
2046 pause 350
2047 enddef

2060 defproc aid
2062 cls_ yellow,black,red
2063 paper #ow,red:print #ow,\"solicitud de prestamo extranjero"
2064 at #ow,12,13:flash #ow,1:print #ow,"espera": pause 50
2066 let x=k$-1
2070 if x: tune "2m1j3f3j3m4r1 2v1t3r3j3l4m":else tune "2g2d3i4d2 2g2d3i4d"
2088 if months<int(rnd*5)+3:goto 2138
2112 if decision$(38+x,1)="*":goto 2128
2113 if group$(7+x,1)<=low:goto 2132

2114 if not x:at #ow,12,0:print #ow,"Los rusos";
2115 if x:at #ow,12,0:print #ow,"Los americanos";
2116 print #ow," te concederan"
2117 let y=group$(7+x,1)*30+int(rnd*200)
2118 at #ow,14,7:print #ow,y;",000 dolares"
2120 let money=money+y
2124 let decision$(38+x,1)="*": goto 2140

2128 at #ow,12,2:print #ow,"Lo sentimos, no hay prestamo": goto 2140

2132 if not x:at #ow,12,12:paper #ow,red:print #ow,"no!   "
2134 if x:at #ow,12,12:paper #ow,blue:print #ow,'"No!"'
2136 get_key: ret
2138 at #ow,12,2:print #ow,"Es demasiado pronto para dar"\"ayudas economicas"

2140 get_key

2141 enddef

2500 defproc decision
2506 cls_ yellow,black,blue
2510 for a=0 to 20: zx_inverse #ow,1:ink #ow,red:print #ow,"********************************": endfor a
2522 at #ow,3,5:zx_bright #ow,1:flash #ow,1:print #ow,"decision presidencial"
2530 ink #ow,white: at #ow,6,1:print #ow,"Para..."
2534 at #ow,8,4:print #ow,"1. ayudar a un grupo      "
2536 at #ow,10,4:print #ow,"2. ayudar a todos         "
2538 at #ow,12,4:print #ow,"3. tus negocios           "
2540 at #ow,14,4:print #ow,"4. subir el tesoro        "
2542 at #ow,16,4:print #ow,"5. dar poder a un grupo   "
2550 get_key: cls #ow
2560 if k$<"1" or k$>"5":goto 2746
2562 if k$="1":let x$="-2"
2564 if k$="2":let x$="35"
2566 if k$="3":let x$="69"
2568 if k$="4":let x$=":<"
2570 if k$="5":let x$="=?"
2572 at #ow,(20-(((code(x$(2))-20)-(code(x$(1))-20))*3))*.5,0
2574 let y=1: let z=1
2578 for a=code(x$(1))-20 to code(x$(2))-20
2580 if decision$(a,1)="*":goto 2592
2582 let z=0
2588 print #ow,y;".": print #ow,decision$(a,18 to ): print
2593 let y=y+1
2594 endfor a
2596 if z:goto 2600
2597 get_key: goto 2620
2600 at #ow,12,3:zx_bright #ow,1:flash #ow,1:print #ow,"Esta seccion esta agotada"
2602 pause 150: goto dec
2628 if k$<"0" or k$>"6":goto dec
2630 if k$<1 or k$>(code(x$(2))-20)+1-(code(x$)-20):goto dec
2634 let r=((code(x$))-20)+k$-1
2635 if decision$(r,1)="*":goto dec
2636 if r=38 or r=39:goto 2670
2642 if r=37:goto 2680
2644 advert
2646 at #ow,4,0:zx_bright #ow,1:print #ow,decision$(r,18 to )
2648 cash_advice
2650 if not c:pause 200: goto dec
2651 at #ow,4,0:zx_bright #ow,1:print #ow,decision$(r,18 to )
2652 get_key_yes
2653 if k$<>"s":goto dec
2654 if r=35:goto 2674
2656 treasure_report: money_transfer
2660 goto 2740

2670 aid: treasure_report: goto 2741
2674 let strength=strength+2: treasure_report
2676 money_transfer_: goto 2740
2680 cls #ow : at #ow,3,0:zx_inverse #ow,1:print #ow,"transferencia a la cuenta suiza"\\\
2685 let x=int(money/2): if x<1:goto 2720
2687 print #ow,"El tesoro tenia $";int(money);",000"
2690 let money_in_switzerland=money_in_switzerland+x: let money=money-x
2692 pause 100
2694 print #ow,\\"$";x;",000 han sido transferidos": goto 2736
2720 at #ow,12,7:print #ow,"ninguna transferencia hecha": pause 100

2740 treasure_report_details_2
2741 plot: get_key
2746 police_report: revolution

2747 enddef

2750 defproc news
2760 if int(rnd*3):ret
2764 let r=int(rnd*6)+44
2768 for a=1 to 6
2770 if not (decision$(r,1)="N"):exit a
2772 let r=r+1: if r=50:let r=44
2774 next a
2776 ret
2777 endfor a
2780 flash #ow,1: cls_ white,black,blue
2781 flash #ow,0: at #ow,10,11:zx_bright #ow,1:print #ow,"noticias"
2782 for a=1 to 10: zx_beep .6,30
2784 cls #ow : at #ow,10,11:flash #ow,1:print #ow,"noticias"
2786 at #ow,14,0:print #ow,decision$(r,18 to )
2788 ink #ow,white: pause 100
2796 money_transfer: plot
2800 police_report
2806 enddef

3000 defproc the_end

3004 if alive:pause 100: goto 3026
3010 pause 50
3012 tune "4d3d1d3d3g1f2f1d2d1d5d"

3026 let x=0
3027 for a=1 to 8: let x=x+group$(a,1)
3030 cls_ yellow,black,cyan
3035 at #ow,3,1:print #ow,"Tu puntuacion como presidente"
3050 print #ow,\" popularidad final - ";to 28;x
3052 print #ow,\" meses en el poder (";months;"x3) - ";to 28;months*3: let x=x+months*3

3058 if alive
3062 print #ow,\" Por estar vivo - ";to 28;alive
3068 print #ow,\" Por el ahorro"\"     ($";money_in_switzerland;",000 /10,000) -";to 28;int(money_in_switzerland/10)
3070 let x=x+alive+int(money_in_switzerland/10)
3071 endif

3074 print #ow,\\\" Tu total es ";to 28;:zx_bright #ow,1:print #ow,x
3082 if x>score:let score=x
3084 print #ow,\"[ La mayor puntuacion es ";score;" ]"
3086 get_key: cls #ow
3090 cls_ black,white,black: at #ow,1,14:flash #ow,1:print #ow,"final"
3092 police_report_step_3: credits: get_key: cls #ow : stop : rem goto 140 old!!! tmp!!!
3093 enddef

3500 defproc national_flag
3504 loc a,height,x,y
3505 let height=11
3506 let x=center_for(18)
3507 let y=8
3510 for a=y to y+height
3524 at #ow,a,x
3525 paper #ow,red
3526 print #ow,"   ";
3527 paper #ow,blue
3530 print #ow,"            ";
3533 paper #ow,green
3534 print #ow,"   "
3535 endfor a
3537 paper #ow,blue:ink #ow,yellow
3540 at #ow,13,13:print #ow,"******"
3541 at #ow,14,13:print #ow,"  **  "
3542 at #ow,15,13:print #ow,"******"
3543 enddef

3600 defproc national_slogan(text$)
3605 loc times
3606 let times=columns*ow_lines/len(text$)
3610 for a=1 to times
3612 print #ow,text$;" ";
3615 zx_beep .01,40-a+rnd*10
3618 endfor a
3620 enddef

4000 defproc title
4001 loc a
4002 cls_ cyan,black,black
4054 national_slogan "Ritimba"
4055 national_flag
4060 for a=1 to 50: zx_beep .03,a
4110 paper #ow,white:ink #ow,black
4115 center #ow,3," Pulsa una tecla y gobernaras "
4122 center #ow,5," Ritimba "
4134 national_anthem
4135 enddef

4138 defproc national_anthem
4139 loc a,pitch,tune$
4140 let tune$="KPKKMKIHK`KMRPOMOP"
4144 cursen #iw
4145 rep listen
4150 for a=1 to len(tune$)
4152 if len(inkey$(#iw)):exit listen
4160 let pitch=code(tune$(a))-80
4170 if pitch=16
4171 rem pause 20:rem this makes INKEY$ to fail in QPC2!!!
4172 else
4180 zx_beep .5,pitch
4181 endif
4190 endfor a
4192 rem pause 30:rem this makes INKEY$ to fail in QPC2!!!
4193 endrep listen
4194 curdis #iw
4195 enddef

4200 defproc war
4208 if group$(5,1)>low:ret
4210 if group$(5,2)<low:ret
4212 if not int(rnd*3)
4213 actual_war
4214 else
4220 flash #ow,1: cls_ black,white,cyan: flash #ow,0
4224 at #ow,6,1:print #ow,"tratado de guerra con Leftoto"
4226 at #ow,10,3:print #ow,"Tu popularidad en Ritimba"
4227 at #ow,12,11: zx_bright #ow,1:print #ow,"aumentara"
4230 rem poke 23886,255: for a=1 to 5: let i=usr 23884: endfor a
4234 for a=1 to 3: war_ a
4236 war_ 6
4237 endif
4240 enddef

4241 defproc war_(a)
4242 if group$(a,1)<"9":let group$(a,1)=group$(a,1)+1
4244 enddef

4250 defproc actual_war

4270 cls_ red,black,black
4271 at #ow,8,7:flash #ow,1:zx_bright #ow,1:print #ow,"leftoto nos invade"
4276 let rs=0:for a=1 to 3: if group$(a,1)>low:let rs=rs+group$(a,2)
4280 if group$(6,1)>low:let rs=rs+group$(6,2)
4282 let rs=rs+strength
4286 at #ow,12,1:print #ow,"La Fuerza de Ritimban es ";rs
4290 let ls=0:for a=1 to 6: if group$(a,1)<=low:let ls=ls+group$(a,2)
4294 at #ow,14,1:print #ow,"La Fuerza de Leftoto es ";ls
4296 at #ow,18,3:zx_bright #ow,1:zx_inverse #ow,1:print #ow,"una corta y decisiva guerra"
4298 fx_war
4300 if ls+int(rnd*3-1)<rs:rem Ritimban wins

4310 zx_border black: cls #ow : at #ow,12,7:zx_bright #ow,1:flash #ow,1:print #ow," leftoto derrotado ": rem let i=usr 23842
4312 let group$(5,2)="0": ret

4313 else:rem Leftoto wins

4320 cls_ black,white,black
4321 at #ow,7,7:zx_bright #ow,1:flash #ow,1:print #ow,"victoria de leftoto"
4323 rem poke 23886,255: for a=1 to 5: let i=usr 23884: endfor a
4324 if not(decision$(36,1)="*" and int(rnd*3))

4326 let alive=0: if decision$(36,1)="*":at #ow,10,0:print #ow,"El motor del Helicoptero se para": pause 80
4327 at #ow,12,4:print #ow,"Eres acusado de ser un"\\"  enemigo del pueblo y..."
4328 pause 30: fx_2: at #ow,18,7:print #ow,"Sumarisimamente ejecutado"
4330 else:rem escape

4340 cls #ow : at #ow,12,3:zx_bright #ow,1:flash #ow,1:print #ow,"escapas en helicoptero!": rem poke 23886,70: let i=usr 23842
4341 let escape=1

4342 endif

4343 endif
4344 enddef

5000 rem tecla _________________________________________________

5001 defproc key_prompt(prompt$,paper_color):rem use RECOL instead!!!
5002 ink #iw,7:paper #iw,paper_color:cls #iw:at #iw,1,(columns-len(prompt$))/2:print #iw,prompt$;
5003 enddef

5005 defproc get_key_yes
5006 at #ow,20,11:paper #ow,white:flash #ow,1: zx_bright #ow,1:print #ow,' "S"= SI ':rem old!!!
5007 get_key
5008 enddef

5009 defproc get_key

5010 rep dont_press_now
5011 if inkey$(#iw)="":exit dont_press_now
5012 endrep dont_press_now

5014 rep press_now

5015 key_prompt "TECLA",red
5016 cursen #iw
5018 let k$=inkey$(#iw,50)
5019 curdis #iw
5020 if k$<>"":exit press_now
5021 zx_beep .01,30

5022 key_prompt "TECLA",blue
5024 cursen #iw
5025 let k$=inkey$(#iw,50)
5026 curdis #iw
5027 if k$<>"":exit press_now
5028 zx_beep .01,20

5029 endrep press_now

5034 if k$=="s":zx_beep .25,40:else zx_beep .25,10
5038 rem if k$="\#226":goto 80:rem ZX "STOP"!!!

5100 enddef

6000 rem data __________________________________________________
6101 DATA "NMHQJLMMMMMPKLMMMPONER MILI OBLIGATORIA          "
6102 DATA "NMMPMJMMMMMNMLMMMREQUISAR TIERRAS PARA TIRO      "
6103 DATA "NCMPLNMLMLMNMNIMMATACAR LAS BASES DE LA GUERILLA "
6104 DATA "NEMPLMMIMLMNMNKMMATACAR BASE GUERRILLA EN LEFTOTO"
6105 DATA "NMMQONMMIMMNMNMMJECHAR AL JEFE DE LA POL.SEC.    "
6106 DATA "NMMPMMMLMIOMMMMMMECHAR A LOS MILITARES RUSOS     "
6107 DATA "NMDQMLMMMMMOLLLMMAUMENTAR LA PAGA DE LAS TROPAS  "
6108 DATA "NAMQLLMLLMMPLLKLMCOMPRAR MAS ARMAS Y MUNICIOMES  "
6109 DATA "NMMLONMMMMMLMMLMMPARAR LOS ABUSOS DEL EJERCITO   "
6110 DATA "NMMMQIMNMMMMOLMMMAUMENTAR EL SALARIO MINIMO      "
6111 DATA "NMPNQOMMIMMNNNNMJACABAR CON LA POLICIA SECRETA   "
6112 DATA "NMMMPKMKMMMMOKMMMPARAR LA INMIGRACION DE LEFTOTO "
6113 DATA "NCELQKMOLNMMNLLMMPONER ESCUELA GRATIS PARA TODOS "
6114 DATA "NMMMQJMNLNMMPJMMLLEGALIZAR LOS SINDICATOS        "
6115 DATA "NMMLQKMNLMMMOLLMMLIBERAR A SU LIDER ENCARCELADO  "
6116 DATA "NMSMPLMMMMMMMMLMMINICIAR UNA LOTERIA PUBLICA     "
6117 DATA "NMMKMPMMMMMLMMMMMPARAR USO MILITAR DE SUS TIERRAS"
6118 DATA "NMMMIQMLMLMMKONMMBAJAR EL SALARIO MINIMO         "
6119 DATA "NWHMMPMNMOIMMNMMMNACIONALIZAR NEGOCIOS AMERICANOS"
6120 DATA "NMRMMPMJMLMMNOMLMTASAR IMPORTACIONES DE LEFTOTO  "
6121 DATA "NMQNNPMMIMMNMNNMKCORTAR LOS GASTOS DE LA POL.SEC."
6122 DATA "NMHMMQMMMMMMMOMMMBAJAR IMPUESTO SOBRE LA TIERRA  "
6123 DATA "NMMKLPMMMMMLLNNMMCEDER TROPA PARA LABRAR TIERRA  "
6124 DATA "NACNNPMJMONMMPMKMCONSTRUIR UN SISTEMA DE RIEGO   "
6125 DATA "NMMQLLMMLMMNMMLMLHACER MINISTRO AL JEFE-EJERCITO "
6126 DATA "NLILQNMOMNMMMMLMMHACER CLINICAS PARA OBREROS     "
6127 DATA "NMMLKQMMLLMLLOMMLDAR PODERES A LOS TERRATENIENTES"
6128 DATA "NRMKMMMQMKNLMMLPMVENDER ARMAS U.S.A. A LEFTOTO   "
6129 DATA "NYMMMLMLMKPMMMMMMVENDER DERECHOS A FIRMAS U.S.A. "
6130 DATA "NMWKMMMMMPJMMMMNMALQUILAR A RUSIA UNA BASE NAVAL "
6131 DATA "NMENPPMMMMMLMMLMMBAJAR LOS IMPUESTOS             "
6132 DATA "NEMPPPMMMMMMMMLMMHACERTE CAMPA#A DE POPULARIDAD  "
6133 DATA "NMUPPPMMDMMONNNMDQUITAR PODERES A LA POL.SEC.    "
6134 DATA "NMGJJJMMUMMLLLLMUAUMENTAR PODERES A LA POL.SEC.  "
6135 DATA "NIMKLLMMLMMKMMMMLAUMENTAR TUS GUARDAESPALDAS    *"
6136 DATA "NAMIIJMMKMMMMMMMMCOMPRAR UN ESCAPE EN HELICOPTERO"
6137 DATA "NMMMMMMMMMMMMMMMMTRANSFERENCIA A UN BANCO SUIZO *"
6138 DATA "NMMMMMMMMMMMMMMMMPEDIR A LOS RUSOS UNA AYUDA     "
6139 DATA "NMMMMMMMMMMMMMMMMPEDIR A LOS AMERICANOS UNA AYUDA"
6140 DATA "NZMNNPMGMKMMMMMMMNACIONALIZAR NEGOCIOS DE LEFTOTO"
6141 DATA "NHMPMMMJMLMRMMKKLCOMPRAR ARMAS PARA EL EJERCITO  "
6142 DATA "NMMMPLMMLMMMRLPMLPERMITIR ASOCIACIONES CAMPESINAS"
6143 DATA "NMMLLPMMLMMLLRLMLDAR A TERRATEN. EJERCITO PRIVADO"
6144 DATA "NMMMMMMMIMMMMMQMIARCHIVOS DE LA POL.SEC. ROBADOS!"
6145 DATA "NMMMMMMMMMMLMMVMMCUBA ENTRENA A LAS GUERILLAS    "
6146 DATA "NMMMMMMMMMMIMMOMNBARRACON DEL EJERCITO HA VOLADO "
6147 DATA "NMMMMMMMMMMMMJMKMPRECIO DE PLATANOS CAE AL 98%   "
6148 DATA "NMMMMMMMMMMMMOMIMCORONEL GOMEZ SE FUGA A LEFTOTO "
6149 DATA "NMMMMMMMMMMMILKMMEPIDEMIA ENTRE LOS CAMPESINOS   "
6150 data "76::$el ejercito"
6151 data "76::'los campesinos"
6152 data "76::+los terratenientes"
6153 data "06--&las guerillas"
6154 data "76--%los leftotos"
6155 data "76--+la policia secreta"
6156 data "7---#los rusos"
6157 data "7---'los americanos"
6200 data "ejercito  ","campesinos","terraten. ","guerillas "
6210 data "leftotos  ","pol.sec.  ","rusos     ","americanos"

7010 defproc s
7011 save "dos1_sb_ritimba_ritimba_bas"
7012 enddef

10000 rem _________________________________________________
10001 rem New for SuperBASIC

11000 defproc zx_border(color):rem tmp!!!
11010 enddef
11020 defproc zx_inverse(channel,flag):rem tmp!!!
11030 enddef
11040 defproc zx_bright(channel,flag):rem tmp!!!
11050 enddef
11070 defproc zx_beep(duration,pitch)
11080 enddef

11400 deffn center_for(width_in_chars)
11405 ret (columns-width_in_chars)/2
11406 enddef

11410 defproc center(channel,line_,text$)
11415 at #channel,line_,center_for(len(text$))
11417 print #channel,text$
11418 enddef 

11500 deffn csize_width_pixels(w)
11502   sel on w
11504     =0:ret 6
11506     =1:ret 8
11508     =2:ret 12
11510     =3:ret 16
11512   endsel
11514 enddef

11516 deffn csize_height_pixels(h)
11518   ret h*10+10
11520 enddef

12000 defproc init_windows

12005 let columns=32
12006 let lines=24

12007 let iw_lines=3
12008 let ow_lines=lines-iw_lines

12009 let ow=fopen("scr_")
12010 csize #ow,3,1
12011 window #ow,columns*16,ow_lines*20,0,0

12015 let iw=fopen("con_")
12016 csize #iw,3,1
12020 window #iw,columns*16,iw_lines*20,0,(lines-iw_lines)*20

12090 let blank_line$=fill$(" ",32)

12100 enddef

13000 defproc init_screen
13010 mode 8
13011 colour_ql
13015 let black=0:let blue=1:let red=2:let purple=3
13016 let green=4:let cyan=5:let yellow=6: let white=7
13050 enddef

14000 defproc init
14010 tk2_ext
14020 init_screen
14030 init_windows
14100 enddef
